plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.spring'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

kotlin {
    jvmToolchain(21)
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

def copyWebAssets = tasks.register('copyWebAssets', Copy) {
    dependsOn ':web:buildWeb'
    from("${project(':web').projectDir}/dist")
    into(layout.buildDirectory.dir('generated/web').get())
}

tasks.named('bootJar') {
    dependsOn copyWebAssets
    from(copyWebAssets.map { it.destinationDir }) {
        into 'BOOT-INF/classes/static'
    }
}
